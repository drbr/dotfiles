#!/bin/zsh

# Launch my installed version of zsh instead of the (older) Mac default
# IMPORTANT: PUT NO CONFIGURATION BEFORE THIS OR ELSE IT WILL GET RUN TWICE!!!!
ZSH=/usr/local/bin/zsh
if [[ $SHELL != $ZSH && -e $ZSH ]]; then
    SHELL=$ZSH
    exec $ZSH "$@"
fi

# Get the directory where the script resides
script_dir=${0:h}

# Put custom stuff on the path. In zsh, path and PATH are linked, so we can do
# this cool array trick. Mac OS X initializes PATH to the values in /etc/paths.
# I add things here because it's more convenient.
path=(
    /usr/local/opt/ruby/bin  # Use Homebrew Ruby instead of Mac Ruby
    /usr/local/bin
    /usr/local/sbin
    $path
)

typeset -ga precmd_functions
typeset -ga preexec_functions

# Colors!
autoload colors
colors

# Basic options (not specific to zsh)
export GREP_COLOR=${GREP_COLOR:-1;35}   # Set the color for grep matches
DIRSTACKSIZE=20                         # number of directories in pushd/popd stack
export EDITOR='vim'
export VISUAL=$EDITOR
export PAGER='less -FMRiX'

# Aliases for common commands
alias grep='nocorrect grep --color=auto'
alias ls='ls -G'
alias ll='ls -lh'
alias la='ls -alh'
alias less='less -MRiX' # note the missing -F option as opposed to $PAGER
alias tmux='TERM=screen-256color-bce tmux' # Set TERM for tmux so color scheme works


# Do something about module_path and fpath if we ever want custom modules or functions


# Format the command prompt with the specified theme
ZSH_THEME="vcs_info"
source $script_dir/themes/$ZSH_THEME


# zsh options
setopt COMPLETE_IN_WORD         # allow tab completion in the middle of a word
setopt ALWAYS_TO_END            # push that cursor on completions.
setopt AUTO_NAME_DIRS           # change directories to variable names
setopt AUTO_PUSHD               # push directories on every cd
setopt NO_BEEP                  # never ever beep ever


# History options
setopt EXTENDED_HISTORY         # store time in history
setopt HIST_EXPIRE_DUPS_FIRST   # unique events are more useful
setopt HIST_VERIFY              # make those history commands nice
setopt INC_APPEND_HISTORY       # immediately insert history into history file
setopt HIST_IGNORE_DUPS         # ignore duplicates from previous event
# setopt SHARE_HISTORY            # share history with all zsh sessions
HISTSIZE=16000
SAVEHIST=15000
HISTFILE=~/.history


# Completion style
# Use compinstall to modify these. See `man zshcompsys` for more info.
zstyle ':completion:*' completer _expand _complete _approximate
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' list-prompt '%SAt %p: Hit TAB for more, or the charater to insert%s'
zstyle ':completion:*' matcher-list '' 'm:{a-z}={A-Z}' 'r:|[._-]=** r:|=**' 'l:|=* r:|=*'
zstyle ':completion:*' menu select=long
zstyle ':completion:*' select-prompt '%SScrolling active: current selection at %p%s'
zstyle ':completion:*' use-compctl true


# Keybindings for line editor
bindkey -v
bindkey "^R" history-incremental-search-backward
bindkey "^E" end-of-line
bindkey "^A" beginning-of-line
bindkey "^T" push-line-or-edit

# VI editing mode is a pain to use if you have to wait for <ESC> to register.
# This times out multi-char key combos as fast as possible. (1/100th of a second)
KEYTIMEOUT=1


# See http://zshwiki.org/home/builtin/functions/zmv
autoload zmv
alias 'zcp=noglob zmv -W -C'
alias 'zln=noglob zmv -W -L'
alias 'zmv=noglob zmv -W -M'


# ssh to specific listed hosts at once
# This command must be run from within a tmux session
mssh() {
    ssh_command="/usr/bin/ssh"
    tmux new-window "$ssh_command $1"
    shift
    while [ ! -z "$1" ]; do
        tmux split-window -h "$ssh_command $1"
        shift
    done
    tmux select-layout tiled
    tmux select-pane -t 0
    tmux set-window-option synchronize-panes on
}


# Pretty-print JSON argument (requires Python 2.6 or higher)
jsonformat() {
    echo $1 | python -mjson.tool
}


# cd up according to argument (e.g. `u 2` -> cd ../..)
u() {
    set -A ud
    ud[1+${1-1}]=
    cd ${(j:../:)ud}
}

# Run this function to set the title of the command prompt
title() {
    echo -ne "\033]0;"$*"\007"
}

autoload -U zutil
autoload -U compinit
autoload -U complist
compinit
